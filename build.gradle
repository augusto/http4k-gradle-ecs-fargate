plugins {
    id 'application'
    id 'org.jetbrains.kotlin.jvm' version '1.4.21'
    id 'com.google.cloud.tools.jib' version '3.1.2'
}

group 'org.example'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib"
    implementation platform("org.http4k:http4k-bom:4.1.1.1")
    implementation "org.http4k:http4k-core"
    implementation "org.http4k:http4k-server-netty"
    implementation "org.http4k:http4k-client-apache"
    implementation "org.http4k:http4k-contract"
    implementation "org.http4k:http4k-format-jackson"

    testImplementation platform("org.junit:junit-bom:5.7.0")
    testImplementation "org.http4k:http4k-testing-hamkrest"
    testImplementation "org.junit.jupiter:junit-jupiter-api"
    testImplementation "org.junit.jupiter:junit-jupiter-engine"
}

application {
    mainClass = 'org.example.AppKt'
}

compileKotlin {
    kotlinOptions {
        jvmTarget = 11
    }
}

test {
    useJUnitPlatform()
}

String awsRegion = "eu-west-2"

jib {
    from {
        image = 'openjdk:15-alpine'
    }
    to {
        image = dockerImageUrl(awsRegion)
        credHelper = 'ecr-login'
        tags = [gitHash()]
    }
    container {
        mainClass = 'org.example.AppKt'
        jvmFlags = ['-Xms512m', '-Xdebug']
        ports = ['9000']
        format = 'Docker'
    }
}

task initTerraform {
    doLast {
        exec {
            workingDir 'infra'
            commandLine '/bin/sh', '-c', 'terraform init'
        }
        exec {
            workingDir 'deploy'
            commandLine '/bin/sh', '-c', 'terraform init'
        }
    }
}

task provisionInfra {
    mustRunAfter 'initTerraform'
    doLast {
        exec {
            workingDir 'infra'
            commandLine '/bin/sh', '-c', "terraform apply -auto-approve -var='aws_region=${awsRegion}'"
        }
    }
}

task deployContainer(dependsOn: 'jib') {
    mustRunAfter 'jib'
    doLast {
        exec {
            workingDir 'deploy'
            commandLine '/bin/sh', '-c', "terraform apply -auto-approve -var='app_image=${dockerImageUrl(awsRegion)}' -var='aws_region=${awsRegion}'"
        }
    }
}

task destroyInfra {
    doLast {
        exec {
            workingDir 'deploy'
            commandLine '/bin/sh', '-c', "terraform destroy -auto-approve -var='aws_region=${awsRegion}'"
        }
        exec {
            workingDir 'infra'
            commandLine '/bin/sh', '-c', "terraform destroy -auto-approve -var='aws_region=${awsRegion}'"
        }
    }
}

static String gitHash() {
    'git rev-parse --verify HEAD'.execute().text.trim()
}

static String dockerImageUrl(String awsRegion) {
    def awsAccount = 'aws sts get-caller-identity --query Account --output text'.execute().text.trim()
    def ecrUrl = "${awsAccount}.dkr.ecr.${awsRegion}.amazonaws.com/hello-world"
    "${ecrUrl}:${gitHash()}"
}